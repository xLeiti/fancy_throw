echo ====================================== FancyThrow v2 ==========================================
echo 																						
echo a config by "Leiti" (youtube.com/@xLeiti)													
echo 																						
echo ===============================================================================================


//bind
bind p +fth


//Set time offsets & +forward?
alias maybe_forward "+forward"
alias set_jumpOffset default_jumpOffset
alias set_throwOffset default_throwOffset


alias default_jumpOffset incrementvar joy_pitch_sensitivity  0 20 1; 
alias default_throwOffset incrementvar joy_pitch_sensitivity  0 36 1

//Pre-Sets
alias fth_set_default "alias maybe_forward +forward; alias set_jumpOffset default_jumpOffset; alias set_throwOffset default_throwOffset"
alias fth_set_default2 "alias maybe_forward; alias set_jumpOffset joy_pitch_sensitivity 0; alias set_throwOffset incrementvar joy_pitch_sensitivity  0 16 1;"
alias fth_set_wjt "alias maybe_forward +forward; alias set_jumpOffset joy_pitch_sensitivity 0; alias set_throwOffset joy_pitch_sensitivity 0;"
alias fth_set_jt "alias maybe_forward; alias set_jumpOffset joy_pitch_sensitivity 0; alias set_throwOffset joy_pitch_sensitivity 0;"

//volume
snd_toolvolume 0.15


//silence log
alias #fth_echo_off "log_flags general inputservice console soundsystem client engineServiceManager +donotecho | grep %"
alias #fth_echo_on "log_flags general inputservice console soundsystem client engineServiceManager -donotecho | grep %"


//convars
setinfo tempvar ""
setinfo timeReader ""
setinfo currentTime "1"
setinfo currentTimeCopy ""
setinfo converter ""
setinfo "-9999999999999999" =

joy_advaxisu 0
joy_advaxisv 0
joy_advaxisz 0

//debug aliases
alias printFrameTime "#fth_echo_on; status_json | grep frametime; #fth_echo_off"
alias printTime "#fth_echo_on; currentTime; #fth_echo_off"
alias printJumping "#fth_echo_on; echo jumping; #fth_echo_off"
alias printThrowing "#fth_echo_on; echo throwing; #fth_echo_off"
alias printOldTick "#fth_echo_on; echo oldTick; #fth_echo_off"
alias printCurrentTick "#fth_echo_on; joy_advaxisu; #fth_echo_off"


//internal aliases
alias +fth "#fth_echo_off; resetMouseSens; get_fps; fps_max 64; bind mouse_x fth; bind mouse_y fth; alias checkJump checkJump2"
alias -fth "reset_fps; bind mouse_x yaw; bind mouse_y pitch; alias fthInit fthInit2; alias initForward; forward -999 0 0; fth_set_default; #fth_echo_on;"

alias fth "newTickDetection;"

alias newTick "getTime; initForward; fthInit; incrementTick; checkJump; checkThrow; //printFrameTime; printCurrentTick; printTime
alias incrementTick "incrementvar joy_advaxisu 0 99999999999999999 1"

alias do_jump "+jump; jump -999 0 0; alias checkJump; alias checkThrow checkThrow2 //printJumping

alias do_throw "attack -999 0 0; attack2 -999 0 0; forward -999 0 0; play buttons/blip2.vsnd; alias checkThrow;  //printThrowing

//temporally store the user's fps_max value
alias get_fps "incrementvar fps_max 0 9999 0 | alias reset_fps;


//Parse number from costum_cvar to game_cvar
alias parseJoy_advaxisuToJoy_pitch_sensitivity  "-9999999999999999 =; joy_pitch_sensitivity  -1; joy_advaxisu | toggle -9999999999999999; -9999999999999999 | multvar joy_pitch_sensitivity ; multvar joy_pitch_sensitivity  -9999999999999999 9999999999999999 -1;"

//NewTickDetection: Multiple Inputs -> 1 Only one execution per tick 

alias newTickDetection "alias [Player cmd; alias 0] newTick; tempvar .;net_channels | tempvar | tempvar; currentTimeCopy =; currentTime | toggle currentTimeCopy; tempvar | toggle currentTimeCopy;toggle currentTimeCopy newTick seconds duplicateTick; currentTimeCopy | bind JOY30; key_findbinding newTick | alias; [Player;"


//GetTime

alias getTime "timeReader .;currentTime :; echo timeReader | net_channels | echo ###################################################################################################### | timeReader | grep |;timeOffsetPusher;timeOffsetPusher;timeOffsetPusher;timeOffsetPusher;timeOffsetPusher;timeOffsetPusher;timeOffsetPusher; timeReader | toggle currentTime"
alias timeOffsetPusher "echo ################################################################################################################################################################################################################################################################### | timeReader | timeReader"


//Init (prepare time offset values for the jump and release action)

alias fthInit fthInit2
alias fthInit2 "alias initForward initForward2; alias fthInit;joy_advaxisu -1"
alias initForward2 "+forward; alias initForward"

//CheckJump

alias checkJump checkJump2
alias checkJump2 "alias [Player cmd; alias 0] do_jump; converter =; parseJoy_advaxisuToJoy_pitch_sensitivity; set_jumpOffset; joy_pitch_sensitivity  | toggle converter; toggle converter cmd 0.000000 do_jump; converter | bind JOY30; key_findbinding do_jump | alias; [Player;"


//CheckThrow

alias checkThrow checkThrow2
alias checkThrow2 "alias [Player cmd; alias 0] do_throw; converter =; parseJoy_advaxisuToJoy_pitch_sensitivity; set_throwOffset; joy_pitch_sensitivity  | toggle converter; toggle converter cmd 0.000000 do_throw; converter | bind JOY30; key_findbinding do_throw | alias; [Player;"